ENUNCIADO:

3) Modificar o programa anterior para que o mesmo também
receba dados e os armazene na memória interna entre os
endereços 30H e 35H. Armazenar de forma cíclica.

RESET 		EQU 00H
LTSERIAL	EQU 23H ; local tratador
STATE 		EQU 20H
F1			EQU PSW.1 ;F1 NÃO É ACEITO PRECISO COLOCA-LO  MANUALMENTE
		
			ORG RESET ;PC=0 depois de reset
			JMP INICIO
			
			ORG LTSERIAL ;AJEITAMOS O TRATADOR DE INTERRUPÇÃO PARA ROTINA DE TRANSMITIR/RECEBER
			JNB RI, TRANSMITIR ;COMPARA RI PARA VER SE ESTA SETADO, RI RECEIVE INTERRUPT
			CLR RI ;LIMPA RI
			SETB F0 ;F0 PROPOSITO GERAL USADO PELO PROGRAMADOR, USAMOS PARA PODER RECEBER/TRANSMITIR
TRANSMITIR: JNB TI, RETORNO ;COMPARA TI PARA VER SE ESTA SETADO, TI TRANSMIT INTERRUPT
 			CLR TI ;LIMPA TI
			SETB F1 ;F1  PROPOSITO GERAL USADO PELO PROGRAMADOR, USAMOS PARA PODER RECEBER/TRANSMITIR
RETORNO:	RETI
			
INICIO: 	MOV IE,#10010000B
			MOV SCON,#11010000B ;MODO 3,  REN = '1' -> PARA RECEBER
			MOV TMOD,#00100000B ;TIMER MODO 2
			MOV TH1,#0FFH ;TABELA MODO 3 PRECISA SER RECARREGADO COM FFH
			MOV TL1,#0FFH ;TABELA MODO 3 PRECISA SER RECARREGADO COM FFH
			MOV PCON,#80H ;10000000B SMOD = '1'
			SETB TR1 ;COMEÇA A CONTAR O TIMER1
			MOV A, #41H ;COLOCA 41H EQUIVALENTE A LETRA A NA TABELA ASCII
			MOV C, P ;COLOCA O BIT DE PARIDADE NO CARRY
			MOV TB8, C ;COLOCA O BIT DE PARIDADE EM TB8 (NONO BIT DE TRANSMISSAO DO MODO 2 E 3)
			MOV SBUF, A ;COLOCA A EM SBUF PARA SAIR NA UART
			MOV R0, #30H ;PARA COMECAR A ESCREVER NO POSIÇÃO DE MEMORIA 30H INTERNA
			
RECEBE: 	JNB F0, TRANSMITE ;COMPARA F0, SE NÃO FOR 1 PULA PARA TRANSMITIR
			CLR F0 ;LIMPA F0
			MOV @R0, SBUF ;MANDA O QUE TEM EM SBUF PARA A POSIÇÃO APONTADA POR R0 (30H)
			INC R0 ;INCREMENTE R0 PARA PROXIMA POSIÇAO DE MEMORIA
			CJNE R0, #36H, TRANSMITE ;LOOP
			MOV R0, #30H ;
			
TRANSMITE:	JNB F1, RECEBE ;SE F1 NÃO FOR 1 PULA PARA RECEBER
			CLR F1 ;LIMPA F1
			INC A ;INCREMENTA A PARA PROXIMAS LETRAS 
			MOV C,P ;JOGA O BIT DE PARIDADE PRO CARRY 
			MOV TB8, C ;JOGA O PARIDADE PRO TB8 NONO BIT DE TRANSMISSAO
			MOV SBUF,A ;JOGA A EM SBUF
			CJNE A,#61, RECEBE 
			MOV A, #40H
			JMP RECEBE
			END

OBS:
(i) F1 NÃO É ACEITO PELO KEIL PRECISAMOS CONFIGURAR MANUALMENTE!